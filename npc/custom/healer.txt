-	script	Buff	-1,{
	.@fullBuffCostItemId = THE_BOX_ITEM_ID;
	.@fullBuffCost = (1 * max(1,fullBuffCostMultiplier));
	.@repairAllPrice = (100000 * max(1,repairAllPriceMultiplier));
	menu
	"ฟื้นฟู HP & SP เต็ม",-
	,"ฟื้นฟู HP & SP & AP เต็ม & รับ Buff & Reset Cooldown [^af1ada" + getitemname(.@fullBuffCostItemId) + " " + F_InsertComma(.@fullBuffCost) + " ชิ้น^000000] (สุ่มราคาใหม่หลังซื้อ)",L_FullBuff
	,"รับ All Status ชั่วคราว จาก Combo",L_Combo
	,"รับ พลังพิเศษ ชั่วคราว จาก Boss Combo",L_BossCombo
	,"ซ่อมอุปกรณ์ทั้งตัว [^af1ada" + F_InsertComma(.@repairAllPrice) + "z^000000]",L_RepairAll
	;
	.@Delay = 3;
	if (@HD > gettimetick(2))
	end;
	specialeffect2 EF_HEAL2;
	percentheal 100,100;
	if (.@Delay)
	@HD = gettimetick(2) + .@Delay;
	end;
	
	L_FullBuff:
	if(countitem(.@fullBuffCostItemId) < .@fullBuffCost){
		mes getitemname(.@fullBuffCostItemId) + " ไม่เพียงพอ";
		close;
	}
	delitem .@fullBuffCostItemId,.@fullBuffCost;
	specialeffect2 509;
	sc_start SC_BLESSING,1800000,10;
	sc_start SC_INCREASEAGI,1800000,10;
	sc_start SC_ANGELUS,1800000,10;
	sc_start SC_IMPOSITIO,1800000,5;
	sc_start SC_KYRIE,1800000,10;
	sc_start SC_SLOWPOISON,1800000,4;
	sc_start SC_SUFFRAGIUM,1800000,3;
	sc_start SC_MAGNIFICAT,1800000,5;
	sc_start SC_GLORIA,1800000,5;
	sc_start SC_ASSUMPTIO,1800000,5;
	sc_start SC_EXPIATIO,1800000,5;
	sc_start SC_RENOVATIO,1800000,4;
	sc_start SC_SECRAMENT,1800000,5;
	//sc_start SC_STRIKING,1800000,5;
	sc_start SC_OVERTHRUST,1800000,5;
	atcommand "@resetcooltime";
	percentheal 100,100;
	healap 10000;
	specialeffect2 77;
	fullBuffCostMultiplier = rand(1,IsWeekend() ? 5 : 10);
	end;
	
	L_Combo:
	setarray .@values[0],1,10,100,1000,10000;
	mes "คุณมี " + combo + " Combo";
	mes "*รับแล้ว Combo จะเหลือเท่าเดิม ไม่ลด";
	next;
	.@comboIndex = select(
	"All status +" + F_InsertComma(.@values[0]) + " ใช้ 10 Combo:"
	+ "All status +" + F_InsertComma(.@values[1]) + " ใช้ 100 Combo:"
	+ "All status +" + F_InsertComma(.@values[2]) + " ใช้ 1,000 Combo:"
	+ "All status +" + F_InsertComma(.@values[3]) + " ใช้ 10,000 Combo:"
	+ "All status +" + F_InsertComma(.@values[4]) + " ใช้ 100,000 Combo:"
	);
	if(combo < pow(10,.@comboIndex)){
		mes "Combo ไม่เพียงพอ";
		close;
	}
	comboPower = .@values[.@comboIndex - 1];
	callsub L_ApplyComboBonuses;
	close;
	
	L_BossCombo:
	setarray .@values[0],1,5,10,25,50;
	mes "คุณมี " + bossCombo + " Boss Combo";
	mes "*รับแล้ว Boss Combo จะเหลือเท่าเดิม ไม่ลด";
	next;
	.@comboIndex = select(
	"All trait +" + F_InsertComma(.@values[0]) + " ใช้ 10 Boss Combo:"
	+ "All trait +" + F_InsertComma(.@values[1]) + " ใช้ 100 Boss Combo:"
	+ "All trait +" + F_InsertComma(.@values[2]) + " ใช้ 1,000 Boss Combo:"
	+ "All trait +" + F_InsertComma(.@values[3]) + " ใช้ 10,000 Boss Combo:"
	+ "All trait +" + F_InsertComma(.@values[4]) + " ใช้ 100,000 Boss Combo:"
	);
	if(bossCombo < pow(10,.@comboIndex)){
		mes "Boss Combo ไม่เพียงพอ";
		close;
	}
	bossComboPower = .@values[.@comboIndex - 1];
	callsub L_ApplyComboBonuses;
	close;
	
	L_ApplyComboBonuses:
	bonus_script_clear;
	bonus_script "bonus bAllStats," + comboPower + ";",86400,1,1;
	bonus_script "bonus bAllTraitStats," + bossComboPower + ";",86400,1,1;
	return;
	
	L_RepairAll:
	if(Zeny < .@repairAllPrice){
		mes "Zeny ไม่เพียงพอ";
		close;
	}
	ChangeZeny(-.@repairAllPrice);
	repairall;
	specialeffect2 101;
	repairAllPriceMultiplier = rand(1,10);
	end;
	
OnInit:
	waitingroom "Buff",0;
	end;
}

// Duplicates
//============================================================
prontera,148,178,7	duplicate(Buff)	จัสมิน#prt	10458 
