-	script	Warp	-1,{
	specialeffect2 509;
	
	ClearTheBoxDebuff();

	menu
	" ~ THE BOX ROOM",TheBoxRoom
	," ~ ทดสอบ Damage",TestDamage
	," ~ PvP [^af1ada" + getmapusers("pvp_n_1-3") + " คน^000000]",Pvp
	," ~ PvP (Party ได้) [^af1ada" + getmapusers("pvp_n_1-5") + " คน^000000]",PvpParty
	," ~ PvP (Guild ได้) [^af1ada" + getmapusers("pvp_n_1-1") + " คน^000000]",PvpGuild
	;
	
TheBoxRoom:
	warp "prontera",156,236;
	end;
	
TestDamage:
	clear;
	mes "ระบุ % ของเสื่อม";
	next;
	input bonusDebuff;
	ApplyTired();
	.@index = (select(" ~ ปกติ: ~ Rank 0: ~ Rank 10: ~ Rank 100: ~ Rank 1000") - 1);
	setarray .@mapName$[0],"tra_fild","06guild_01","06guild_02","06guild_03","06guild_04";
	warp .@mapName$[.@index],0,0;
	end;
	
Pvp:
	warp "pvp_n_1-3",0,0;
	end;

PvpParty:
	warp "pvp_n_1-5",0,0;
	end;

PvpGuild:
	warp "pvp_n_1-1",0,0;
	end;
}

prontera,161,199,4	duplicate(Warp)	Warp#1	10558

-	script	TheBoxRoom	-1,{
	specialeffect2 509;
	
	ClearTheBoxDebuff();
	
	BotMacroHint();
	
	.@difficulty = strnpcinfo(2);
	
	.@index = 0 + (5 * (.@difficulty - 1));
	
	for(.@i = .@index; .@i < (.@index + 5); .@i++){
		// Information - Monster ID
		setarray .@monsterIds[0]
		,getd("$tbrMID" + .@i + "[0]")
		,getd("$tbrMID" + .@i + "[1]")
		,getd("$tbrMID" + .@i + "[2]")
		,getd("$tbrMID" + .@i + "[3]")
		;
		
		// Information - Monster name
		setarray .@monsterNames$[0]
		,"^d8601b" + getmonsterinfo(.@monsterIds[0],MOB_NAME) + "^000000"
		,"^d8601b" + getmonsterinfo(.@monsterIds[1],MOB_NAME) + "^000000"
		,"^d8601b" + getmonsterinfo(.@monsterIds[2],MOB_NAME) + "^000000"
		,"^d8601b" + getmonsterinfo(.@monsterIds[3],MOB_NAME) + "^000000"
		;
		
		// Tell player here
		mes "^1c9311ห้อง " + (.@i + 1) + "^000000"
		+ "^000000\n " + .@monsterNames$[0] + " (" + .@monsterIds[0] + ")"
		+ "^000000\n " + .@monsterNames$[1] + " (" + .@monsterIds[1] + ")"
		+ "^000000\n " + .@monsterNames$[2] + " (" + .@monsterIds[2] + ")" 
		+ "^000000\n " + .@monsterNames$[3] + " (" + .@monsterIds[3] + ")"
		+ "^000000\n ^d32323" + getmonsterinfo(getd("$tbrBMID" + .@i),MOB_NAME) + "^000000 (" + getd("$tbrBMID" + .@i) + ")"+ " " + "(โอกาสพบ 0.01%)"
		;
		
		// Menu creator
		.@tbrMenu$ += " ~ เข้าห้องที่ " + (.@i + 1) + " (ระดับความยาก " + .@difficulty + ") [EXP ^00aaff" + KM($expTiers[.@difficulty - 1] * getd("$tbrExpMultiplier" + .@i)) + "^000000, Drop x " + getd("$tbrDropMultiplier" + .@i) + "]:";
	}
	
	// Select room 1~5 from each tier
	.@index = (select(.@tbrMenu$) - 1);
	debugmes "Select room " + (.@index + 1);
	// Get actual map index
	.@room = .@index * (cap_value(.@difficulty - 1,1,INT_MAX));
	debugmes "Map index " + .@room;
	// Remember last play map index
	lastPlayMap = .@room;
	
	// Remember last play map name
	lastPlayMap$ = $maps$[$playingMaps[.@room]];
	debugmes "Map " + lastPlayMap$;
	// Remember last play map tier
	tier = .@difficulty;
	debugmes "Tier " + tier;
	// Warp now
	warp lastPlayMap$,0,0;
	
	// Spawn if need
	if(!$isMapSpawnMonster[lastPlayMap]){
		callsub OnSpawnMore,lastPlayMap,$tbrSpawnAmount,1,0;
		
		$isMapSpawnMonster[lastPlayMap] = 1;	
	}
	
	end;
	
OnSpawnMore:
	.@index = getarg(0,0);
	.@amount = getarg(1,0);
	.@isNonBoss = getarg(2,1);
	.@isBoss = getarg(3,1);
	if(.@isNonBoss){
		freeloop(1);
		for(.@i = 0; .@i < .@amount; .@i++)
		monster lastPlayMap$,0,0,"--ja--",getd("$tbrMID" + .@index + "[" + rand(0,3) + "]"),1,strnpcinfo(0) + "::OnMonsterDead";
		freeloop(0);
	}
	if(.@isBoss && !$isTheBoxRoomBossDead[.@index])
	monster lastPlayMap$,0,0,"--ja--",getd("$tbrBMID" + .@index),1,strnpcinfo(0) + "::OnBossDead";
	return;

OnMonsterDead: callsub OnGetReward,0; callsub OnSpawnMore,lastPlayMap,1,1,0; end;
OnBossDead: callsub OnGetReward,1; $isTheBoxRoomBossDead[lastPlayMap] = 1; end;

OnGetReward:
	.@isBoss = getarg(0,0);
	// Reward EXP
	return;
	
OnInit:
	waitingroom strnpcinfo(0),0;
	end;
}

prontera,148,238,5	duplicate(TheBoxRoom)	THE BOX ROOM#1	10558
prontera,163,238,3	duplicate(TheBoxRoom)	THE BOX ROOM#2	10558
