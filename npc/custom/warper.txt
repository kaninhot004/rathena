-	script	Warp	-1,{
	getmapxy(.@map$,.@x,.@y,BL_PC);
	savepoint .@map$,.@x,.@y,2,2;
	
	specialeffect2 509;
	
	ClearTheBoxDebuff();

	menu
	" ~ THE BOX ROOM",TheBoxRoom
	," ~ ห้องที่ไปล่าสุด",LastTheBoxRoom
	," ~ ทดสอบ Damage",TestDamage
	," ~ PvP [^af1ada" + getmapusers("pvp_n_1-3") + " คน^000000]",Pvp
	," ~ PvP (Party ได้) [^af1ada" + getmapusers("pvp_n_1-5") + " คน^000000]",PvpParty
	," ~ PvP (Guild ได้) [^af1ada" + getmapusers("pvp_n_1-1") + " คน^000000]",PvpGuild
	;
	
TheBoxRoom:
	warp "prontera",156,236;
	end;
	
LastTheBoxRoom:
	addtimer 1,"TheBoxRoom::OnFastJoin";
	end;
	
TestDamage:
	clear;
	mes "ระบุ % ของเสื่อม";
	next;
	input bonusDebuff;
	ApplyTired();
	.@index = (select(" ~ ปกติ: ~ Rank 0: ~ Rank 3: ~ Rank 66: ~ Rank 99") - 1);
	setarray .@mapName$[0],"tra_fild","06guild_01","06guild_02","06guild_03","06guild_04";
	warp .@mapName$[.@index],0,0;
	end;
	
Pvp:
	clear;
	mes "ระบุ % ล้า";
	next;
	input tired;
	ApplyTired();
	warp "pvp_n_1-3",0,0;
	end;

PvpParty:
	warp "pvp_n_1-5",0,0;
	end;

PvpGuild:
	warp "pvp_n_1-1",0,0;
	end;
}

prontera,161,199,4	duplicate(Warp)	Warp#1	10558

-	script	TheBoxRoom	-1,{
	specialeffect2 509;
	
	ClearTheBoxDebuff();
	
	BotMacroHint();
	
	.@difficulty = strnpcinfo(2);
	
	.@index = 0 + (5 * (.@difficulty - 1));
	
	.@tbItemName$ = getitemname(THE_BOX_ITEM_ID);
	.@rhpItemName$ = getitemname(REFINE_HACK_POWDER_ITEM_ID);
	
	for(.@i = .@index; .@i < (.@index + 5); .@i++){
		// Information - Monster ID
		setarray .@monsterIds[0]
		,getd("$tbrMID" + .@i + "[0]")
		,getd("$tbrMID" + .@i + "[1]")
		,getd("$tbrMID" + .@i + "[2]")
		,getd("$tbrMID" + .@i + "[3]")
		;
		
		// Information - Monster name
		setarray .@monsterNames$[0]
		,"^d8601b" + getmonsterinfo(.@monsterIds[0],MOB_NAME) + "^000000"
		,"^d8601b" + getmonsterinfo(.@monsterIds[1],MOB_NAME) + "^000000"
		,"^d8601b" + getmonsterinfo(.@monsterIds[2],MOB_NAME) + "^000000"
		,"^d8601b" + getmonsterinfo(.@monsterIds[3],MOB_NAME) + "^000000"
		;
		
		// Information - Monster level
		setarray .@monsterLevels[0]
		,getmonsterinfo(.@monsterIds[0],MOB_LV)
		,getmonsterinfo(.@monsterIds[1],MOB_LV)
		,getmonsterinfo(.@monsterIds[2],MOB_LV)
		,getmonsterinfo(.@monsterIds[3],MOB_LV)
		;
		
		// Information - Monster EXP
		setarray .@monsterEXPs[0]
		,getmonsterinfo(.@monsterIds[0],MOB_BASEEXP)
		,getmonsterinfo(.@monsterIds[1],MOB_BASEEXP)
		,getmonsterinfo(.@monsterIds[2],MOB_BASEEXP)
		,getmonsterinfo(.@monsterIds[3],MOB_BASEEXP)
		;
		
		// Tell player here
		.@bossHint$ = ((getd("$tbrBossRate" + .@i) > 0) ? ("^000000\n ^d32323" + getmonsterinfo(getd("$tbrBMID" + .@i),MOB_NAME) + "^000000 (" + getd("$tbrBMID" + .@i) + ") (^ff0000Rank " + (.@difficulty * $bossRankMultiplier) + "^000000)\n-> โอกาสเกิด " + StringBase10000(getd("$tbrBossRate" + .@i)) + "%\n-> ^b600ff" + .@rhpItemName$ + " " +  StringBase10000(.@difficulty * $rhpMultiplier) + "%^000000") : "");
		mes "^1c9311---------- ห้อง " + (.@i + 1) + " ----------^000000"
		+ "^000000\n " + .@monsterNames$[0] + " (" + .@monsterIds[0] + ") ^818181Lv. " + .@monsterLevels[0] + "^000000\n[^00aaffEXP " + KM(.@monsterEXPs[0] + ($expTiers[.@difficulty - 1] * getd("$tbrExpMultiplier" + .@i + "[0]"))) + "^000000, ^bb85ff" + .@tbItemName$ + " " + StringBase10000($tbDropRateTiers[.@difficulty - 1] * getd("$tbrDropMultiplier" + .@i + "[0]")) + "%^000000, ^7a40e6" + ($zenyTiers[.@difficulty - 1] * getd("$tbrZenyMultiplier" + .@i + "[0]")) +"z^000000]"
		+ "^000000\n " + .@monsterNames$[1] + " (" + .@monsterIds[1] + ") ^818181Lv. " + .@monsterLevels[1] + "^000000\n[^00aaffEXP " + KM(.@monsterEXPs[1] + ($expTiers[.@difficulty - 1] * getd("$tbrExpMultiplier" + .@i + "[1]"))) + "^000000, ^bb85ff" + .@tbItemName$ + " " + StringBase10000($tbDropRateTiers[.@difficulty - 1] * getd("$tbrDropMultiplier" + .@i + "[1]")) + "%^000000, ^7a40e6" + ($zenyTiers[.@difficulty - 1] * getd("$tbrZenyMultiplier" + .@i + "[1]")) +"z^000000]"
		+ "^000000\n " + .@monsterNames$[2] + " (" + .@monsterIds[2] + ") ^818181Lv. " + .@monsterLevels[2] + "^000000\n[^00aaffEXP " + KM(.@monsterEXPs[2] + ($expTiers[.@difficulty - 1] * getd("$tbrExpMultiplier" + .@i + "[2]"))) + "^000000, ^bb85ff" + .@tbItemName$ + " " + StringBase10000($tbDropRateTiers[.@difficulty - 1] * getd("$tbrDropMultiplier" + .@i + "[2]")) + "%^000000, ^7a40e6" + ($zenyTiers[.@difficulty - 1] * getd("$tbrZenyMultiplier" + .@i + "[2]")) +"z^000000]"
		+ "^000000\n " + .@monsterNames$[3] + " (" + .@monsterIds[3] + ") ^818181Lv. " + .@monsterLevels[3] + "^000000\n[^00aaffEXP " + KM(.@monsterEXPs[3] + ($expTiers[.@difficulty - 1] * getd("$tbrExpMultiplier" + .@i + "[3]"))) + "^000000, ^bb85ff" + .@tbItemName$ + " " + StringBase10000($tbDropRateTiers[.@difficulty - 1] * getd("$tbrDropMultiplier" + .@i + "[3]")) + "%^000000, ^7a40e6" + ($zenyTiers[.@difficulty - 1] * getd("$tbrZenyMultiplier" + .@i + "[3]")) +"z^000000]"
		+ .@bossHint$
		;
		
		.@averageExp = ((.@monsterEXPs[0] + .@monsterEXPs[1] + .@monsterEXPs[2] + .@monsterEXPs[3])  / 4);
		.@averageExpMultiplier = ((getd("$tbrExpMultiplier" + .@i + "[0]") + getd("$tbrExpMultiplier" + .@i + "[1]") + getd("$tbrExpMultiplier" + .@i + "[2]") + getd("$tbrExpMultiplier" + .@i + "[3]")) / 4);
		.@averageZenyMultiplier = ((getd("$tbrZenyMultiplier" + .@i + "[0]") + getd("$tbrZenyMultiplier" + .@i + "[1]") + getd("$tbrZenyMultiplier" + .@i + "[2]") + getd("$tbrZenyMultiplier" + .@i + "[3]")) / 4);
		.@averageDrop = ((getd("$tbrDropMultiplier" + .@i + "[0]") + getd("$tbrDropMultiplier" + .@i + "[1]") + getd("$tbrDropMultiplier" + .@i + "[2]") + getd("$tbrDropMultiplier" + .@i + "[3]")) / 4);
		.@averageLv = ((.@monsterLevels[0] + .@monsterLevels[1] + .@monsterLevels[2] + .@monsterLevels[3])  / 4);
		
		// Menu creator
		.@tbrMenu$ += "ห้อง " + (.@i + 1) + " (ยาก " + .@difficulty + ") [เฉลี่ย Lv. " + .@averageLv + ", EXP ^00aaff" + KM(.@averageExp + ($expTiers[.@difficulty - 1] * .@averageExpMultiplier)) + "^000000, ^bb85ffTB " + StringBase10000($tbDropRateTiers[.@difficulty - 1] * .@averageDrop) + "%^000000, " + ($zenyTiers[.@difficulty - 1] * .@averageZenyMultiplier) + "z] " + $maps$[$playingMaps[.@i]] + " " + ((((.@i + 1) % 2) == ($isCursedRoomEven ? 0 : 1)) ? "^ff0000(คำสาป)^000000" : "") + ":";
	}
	// Select room 1~5 from each tier
	.@index = (select(.@tbrMenu$) - 1);
	// Get actual map index
	.@room = (.@index) + (5 * (.@difficulty - 1));
	// Remember last play map index
	lastPlayMap = .@room;
	// Remember last play map tier
	tier = .@difficulty;
OnFastJoin:
	// Remember last play map name
	lastPlayMap$ = $maps$[$playingMaps[lastPlayMap]];
	// Remember cursed room
	isCursed = (((lastPlayMap + 1) % 2) == ($isCursedRoomEven ? 0 : 1)) ? 1 : 0;
	// Add x DROP
	.@min = gettime(DT_MINUTE);
	while(.@min > 10)
	.@min -= 10;
	sc_start SC_ITEMBOOST,60 * (1000 * (10 - ((.@min == 10) ? 0 : .@min))),$tbrNormalDropMultiplier[lastPlayMap];
	
	if((tier % 2) == 0)
	{
		tired = 50;
		bonusDebuff = 50;
		ApplyTired();
	}

	// Spawn if need
	if(!$isMapSpawnMonster[lastPlayMap]){
		callsub OnSpawnMore,lastPlayMap,$tbrSpawnAmount;
		
		$isMapSpawnMonster[lastPlayMap] = 1;	
	}
	
	addtimer 2000,strnpcinfo(0) + "::OnWarped";

	// Warp now
	warp lastPlayMap$,0,0;
	
	end;
	
OnWarped:
	if(!playerattached())
	end;

	.@min = gettime(DT_MINUTE);
	while(.@min > 10)
	.@min -= 10;
	dispbottom "เหลือเวลาอีก " + (10 - ((.@min == 10) ? 0 : .@min)) + " นาที ก่อนที่ Map จะถูก Reset";

	if($isBossAlive[lastPlayMap]){
		announce getmonsterinfo(getd("$tbrBMID" + lastPlayMap),MOB_NAME) + " ยังมีชีวิตอยู่..",bc_self;
		getunitdata($tbrBossGID[lastPlayMap],.@bossDatas);
		viewpoint 1,.@bossDatas[UMOB_X],.@bossDatas[UMOB_Y],1,0xFF0000;
	}
	
	end;
	
OnSpawnMore:
	.@room = getarg(0,0);
	.@amount = getarg(1,0);
	.@isNonBoss = getarg(2,1);
	.@isBoss = getarg(3,0);
	
	if(.@isNonBoss){
		freeloop(1);
		for(.@i = 0; .@i < .@amount; .@i++){
			.@monsterIndex = rand(0,3);
			monster lastPlayMap$,0,0,"--ja--",getd("$tbrMID" + .@room + "[" + .@monsterIndex + "]"),1,"TheBoxRoom::OnMonsterDeadIndex" + .@monsterIndex;
		}
		freeloop(0);
	}
	if(.@isBoss && !$isBossAlive[.@room]){
		monster lastPlayMap$,0,0,"--ja--",getd("$tbrBMID" + .@room),1,"TheBoxRoom::OnBossDead";
		$tbrBossGID[lastPlayMap] = $@mobid[0];
		setunitdata $tbrBossGID[lastPlayMap],UMOB_RANK,tier * $bossRankMultiplier;
		$isBossAlive[.@room] = 1;
		announce getmonsterinfo(getd("$tbrBMID" + lastPlayMap),MOB_NAME) + " เกิดแล้ว!",bc_map;
	}
	
	return;

OnMonsterDeadIndex0: if(!playerattached()) end; callsub OnGetReward,0; callsub OnSpawnMore,lastPlayMap,1,1,0; callsub OnTryToSpawnBoss; end;
OnMonsterDeadIndex1: if(!playerattached()) end; callsub OnGetReward,1; callsub OnSpawnMore,lastPlayMap,1,1,0; callsub OnTryToSpawnBoss; end;
OnMonsterDeadIndex2: if(!playerattached()) end; callsub OnGetReward,2; callsub OnSpawnMore,lastPlayMap,1,1,0; callsub OnTryToSpawnBoss; end;
OnMonsterDeadIndex3: if(!playerattached()) end; callsub OnGetReward,3; callsub OnSpawnMore,lastPlayMap,1,1,0; callsub OnTryToSpawnBoss; end;
OnBossDead:
	announce strcharinfo(0) + " ได้กำจัด " + getmonsterinfo(getd("$tbrBMID" + lastPlayMap),MOB_NAME) + " ลงแล้ว!",bc_map;
	callsub OnGetReward,0,1;
	$isBossAlive[lastPlayMap] = 0;
	end;

OnTryToSpawnBoss:
	// Boss still alive
	if($isBossAlive[lastPlayMap])
	return;

	if(rand(10000) < getd("$tbrBossRate" + lastPlayMap)){
		callsub OnSpawnMore,lastPlayMap,1,0,1;
		
		getunitdata($tbrBossGID[lastPlayMap],.bossDatas);
		addrid(1,1);
		viewpoint 1,.bossDatas[UMOB_X],.bossDatas[UMOB_Y],1,0xFF0000;
	}
	
	return;

OnGetReward:
	.@timeTick = gettimetick(2);

	// Cursed Room (Delay 1s too)
	if(isCursed && ((@cursedDropDelay < .@timeTick) || .@isBoss)){
		@cursedDropDelay = .@timeTick + 1;
		.@randCursed = rand(100);
		
		if(.@randCursed < 1) // 1% THE BOX KEY
		{
			getitem THE_BOX_ITEM_ID,1;
			message strcharinfo(0),"ร่ำรวย THE BOX!!";
		}
		else if(.@randCursed < 2) // 1% 100k Zeny
		{
			ChangeZeny(100000,0);
			message strcharinfo(0),"ร่ำรวยเงินทอง!!";
		}
		else if(.@randCursed < 30) // 28% +1 Multiplier
		{
			.@cursedMultiplier = 1;
			message strcharinfo(0),"คูณรางวัล!!";
		}
		else if(.@randCursed < 35) // 5% Max move speed (5s)
		{
			bonus_script "bonus bSpeedAddRate,1000;",10,1,1;
			message strcharinfo(0),"ก้าวพริบตา!!";
		}
		else if(.@randCursed < 40) // 5% Max attack speed (5s)
		{
			bonus_script "bonus bAspd,199;",10,1,1;
			message strcharinfo(0),"สับแหลก!!";
		}
		else if(.@randCursed < 65) // 25% Random debuff (3s)
		{
			setarray .@debuffs[0],SC_STONE,SC_FREEZE,SC_STUN,SC_SLEEP,SC_POISON,SC_CURSE,SC_SILENCE,SC_CONFUSION,SC_BLIND,SC_BLEEDING,SC_PROVOKE,SC_DECREASEAGI;
			setarray .@debuffDurations[0],1000,1000,1000,1000,30000,30000,30000,3000,30000,30000,30000,30000;
			.@randDebuff = rand(getarraysize(.@debuffs));
			sc_start .@debuffs[.@randDebuff],.@debuffDurations[.@randDebuff] / 2,10;
		}
		// Other than that nothing
	}
	
	.@monsterIndex = getarg(0,0);
	.@isBoss = getarg(1,0);
	
	// Reward Zeny
	ChangeZeny($zenyTiers[tier - 1] * (.@cursedMultiplier + (.@isBoss ? 10 : getd("$tbrZenyMultiplier" + lastPlayMap + "[" + .@monsterIndex + "]"))),0);
	
	// Reward EXP
	.@rewardExp = $expTiers[tier - 1] * (.@cursedMultiplier + (.@isBoss ? 10 : getd("$tbrExpMultiplier" + lastPlayMap + "[" + .@monsterIndex + "]")));
	getexp .@rewardExp,.@rewardExp / 2;
	
	// Reward Refine Hack Powder ( Difficult / 4 ) %
	if(.@isBoss){
		.@rhpRand = rand(10000);
		if(.@rhpRand < (tier * $rhpMultiplier))
		getitem REFINE_HACK_POWDER_ITEM_ID,1;
		else
		dispbottom "ต้องสุ่มได้ < " + (tier * $rhpMultiplier) + " เพื่อรับ " + getitemname(REFINE_HACK_POWDER_ITEM_ID) + " คุณสุ่มได้ " + .@rhpRand;
	}
	
	// Reward THE BOX
	if ((@tbDropDelay < .@timeTick) || .@isBoss){
		.@tbDropChance = $tbDropRateTiers[tier - 1] * (.@cursedMultiplier + getd("$tbrDropMultiplier" + lastPlayMap + "[" + .@monsterIndex + "]"));
		.@tbRand = rand(10000);
		if((.@tbRand < .@tbDropChance) || .@isBoss){
			@tbDropDelay = .@timeTick + 1;
			getitem THE_BOX_ITEM_ID,.@isBoss ? rand(1,max(2,getd("$tbrBossRate" + lastPlayMap))) : 1;
		}
		else
		dispbottom "ต้องสุ่มได้ < " + .@tbDropChance + " เพื่อรับ " + getitemname(THE_BOX_ITEM_ID) + " คุณสุ่มได้ " + .@tbRand;
	}
	
	// Combo
	if ((@comboDelay < .@timeTick) || .@isBoss){
		combo++;
		@comboDelay = .@timeTick + 1;
		if(#IS_COMBO_INFO)
		dispbottom "Combo " + combo;
	}
	
	return;
	
OnInit:
	$rhpMultiplier = 200;
	$bossRankMultiplier = 2;
	waitingroom strnpcinfo(0),0;
	end;
}

prontera,148,232,5	duplicate(TheBoxRoom)	ห้อง#1	10558
prontera,163,232,3	duplicate(TheBoxRoom)	ห้อง#2	10558
prontera,143,236,5	duplicate(TheBoxRoom)	ห้อง#3	10558
prontera,168,236,3	duplicate(TheBoxRoom)	ห้อง#4	10558
prontera,148,240,5	duplicate(TheBoxRoom)	ห้อง#5	10558
prontera,163,240,3	duplicate(TheBoxRoom)	ห้อง#6	10558
prontera,143,244,5	duplicate(TheBoxRoom)	ห้อง#7	10558
prontera,168,244,3	duplicate(TheBoxRoom)	ห้อง#8	10558
prontera,148,248,5	duplicate(TheBoxRoom)	ห้อง#9	10558
prontera,163,248,3	duplicate(TheBoxRoom)	ห้อง#10	10558
prontera,143,252,5	duplicate(TheBoxRoom)	ห้อง#11	10558
prontera,168,252,3	duplicate(TheBoxRoom)	ห้อง#12	10558
prontera,148,256,5	duplicate(TheBoxRoom)	ห้อง#13	10558
prontera,163,256,3	duplicate(TheBoxRoom)	ห้อง#14	10558
prontera,143,260,5	duplicate(TheBoxRoom)	ห้อง#15	10558
prontera,168,260,3	duplicate(TheBoxRoom)	ห้อง#16	10558
prontera,148,264,5	duplicate(TheBoxRoom)	ห้อง#17	10558
prontera,163,264,3	duplicate(TheBoxRoom)	ห้อง#18	10558
prontera,143,268,5	duplicate(TheBoxRoom)	ห้อง#19	10558
prontera,168,268,3	duplicate(TheBoxRoom)	ห้อง#20	10558
