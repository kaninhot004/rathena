// Monster spawn at [10,15,20,25,30,35,40,45,50,55] -> Player pass their day in map -> +1 amount to spawn (Maximum at 200) (Respawn every 60s)
// In case someone lose -> Reduce bonus spawn amount by half
vr_pop3,174,163,3	script	เริ่มเกม#0	10007,{
	percentheal 100,100;
	.@mapIndex = atoi(strnpcinfo(2)); // Map index
	.@minimumSpawnAmount = (30 + (5 * .@mapIndex)); // Minimum spawn amount
	mes "วิธีเล่น";
	mes "1.) ^1EBA38มีชีวิตรอด^000000 รับ ^B427F5" + F_InsertComma($fcpRewards[.@mapIndex]) + "^000000 FCP";
	mes "2.) ^E6870Eกำจัด Monster^000000 รับ ^16BABA" + F_InsertComma($cpRewards[.@mapIndex]) + "^000000 CP";
	mes "3.) ตาย ^FF0000Stats -" + $deadStatusPenalty[.@mapIndex] + "^000000, ^FF0000Traits -" + $deadTraitPenalty[.@mapIndex] + ", Grade & Refine เหลือ 0^000000";
	mes "4.) Reset Map ทุก ๆ 1 นาที";
	mes "5.) Reset จำนวน Monster ทุกต้นชั่วโมง";
	next;
	menu "เข้า (จำนวน Monster ขณะนี้ ^FF0000" + (.@minimumSpawnAmount + $bonusSpawnAmount[.@mapIndex]) + "^000000 ตัว)",-;
	warp $maps$[.@mapIndex],0,0;
	ClearAddTimer();
	addtimer 60000,strnpcinfo(3) + "::OnSurvive";
	sc_start SC_ITEMBOOST,60000,1;
	addtimer 5000,strnpcinfo(3) + "::OnHint";
	end;
	
OnHint:
	dispbottom "มีชีวิตรอดเพื่อรับ " + F_InsertComma($fcpRewards[.@mapIndex]) + "^000000 FCP (ดูเวลาทางขวาจอได้โดยเอา Mouse ชี้ Icon ชื่อ Item เหลือง ๆ)",0x1EBA38;
	end;
	
OnMinute00:
	.@mapIndex = atoi(strnpcinfo(2)); // Map index
	$bonusSpawnAmount[.@mapIndex] = 0; // Reset bonus spawn amount
	end;
	
OnSurvive:
	// Check player
	if(playerattached()){
		// Check player alive
		if(!isdead()){
			.@mapIndex = atoi(strnpcinfo(2)); // Map index
			// Check map
			if(strcharinfo(3) == $maps$[.@mapIndex]){
				// Give free cash point
				#KAFRAPOINTS = cap_value(#KAFRAPOINTS + $fcpRewards[.@mapIndex],0,INT_MAX);
				dispbottom "คงเหลือ " + F_InsertComma(#KAFRAPOINTS) + " FCP",0xB427F5;
				addtimer 60000, strnpcinfo(3) + "::OnSurvive";
				sc_start SC_ITEMBOOST,60000,1;
				$bonusSpawnAmount[.@mapIndex] = cap_value($bonusSpawnAmount[.@mapIndex] + 1,0,INT_MAX); // increase bonus spawn amount
				
				// 10% Able to spawn 2x2 cell near player
				if(rand(100) < 10){
					.@variableName$ = "$attackableMonsterTier" + (.@mapIndex + 1) + "Ids";
					.@monsterId = getd(.@variableName$ + "[" + rand(getarraysize(getd(.@variableName$))) + "]");
					
					// Get Player Cell
					getmapxy(.@mapName$,.@baseX,.@baseY);
					.@cellSize = 2;
					getfreecell(.@mapName$,.@spawnX,.@spawnY,.@baseX,.@baseY,.@cellSize,.@cellSize);

					monster $maps$[.@mapIndex],.@spawnX,.@spawnY,"--ja--",.@monsterId,1,strnpcinfo(0) + "::OnMonsterDead";
					.@GID = $@mobid[0];
					getunitdata .@GID,.@md;
					setunitdata .@GID,UMOB_MODE,.@md[UMOB_MODE] | MD_CANMOVE | MD_AGGRESSIVE | MD_CANATTACK;
				}
			}
		}
	}
	
	end;
	
	// Check to respawn every 60s
OnTimer60000:
OnSpawn:
	initnpctimer;
	.@mapIndex = atoi(strnpcinfo(2)); // Map index
	.@minimumSpawnAmount = (30 + (5 * .@mapIndex)); // Minimum spawn amount
	// Target amount to spawn
	.@targetAmountToSpawn = cap_value(.@minimumSpawnAmount + $bonusSpawnAmount[.@mapIndex],.@minimumSpawnAmount,$maximumSpawnAmount);
	mapannounce $maps$[.@mapIndex],"จำนวน Monster ขณะนี้ " + .@targetAmountToSpawn + " ตัว",bc_map,0xE6870E;
	// Clear monster
	killmonsterall $maps$[.@mapIndex];
	// Spawn new monster
	freeloop(1);
	.@variableName$ = "$attackableMonsterTier" + (.@mapIndex + 1) + "Ids";
	for(.@i = 0; .@i < .@targetAmountToSpawn; .@i++)
	{
		.@monsterId = getd(.@variableName$ + "[" + rand(getarraysize(getd(.@variableName$))) + "]");
		monster $maps$[.@mapIndex],0,0,"--ja--",.@monsterId,1,strnpcinfo(0) + "::OnMonsterDead";
		.@GID = $@mobid[0];
		getunitdata .@GID,.@md;
		setunitdata .@GID,UMOB_MODE,.@md[UMOB_MODE] | MD_CANMOVE | MD_AGGRESSIVE | MD_CANATTACK;
	}
	freeloop(0);
	
	end;
	
OnMonsterDead:
	// Check player
	if(playerattached())
	{
		.@mapIndex = atoi(strnpcinfo(2)); // Map index
		// Check map
		if(strcharinfo(3) == $maps$[.@mapIndex]){
			// Give cash point
			#CASHPOINTS = cap_value(#CASHPOINTS + $cpRewards[.@mapIndex],0,INT_MAX);
			dispbottom "คงเหลือ " + F_InsertComma(#CASHPOINTS) + " CP",0x16BABA;
			// Potion
			if(rand(100) < $potionDropRate){
				getitem $potionDrops[.@mapIndex],1;
			}
			// Blue Potion
			if(rand(100) < $bluePotionDropRate){
				getitem $bluePotionItemId,1;
			}
			// Green Potion
			if(rand(100) < $greenPotionDropRate){
				getitem $greenPotionItemId,1;
			}
		}
	}
	end;

OnPCDieEvent:
	// Check player
	if(playerattached())
	{
		ClearAddTimer();
		.@mapIndex = atoi(strnpcinfo(2)); // Map index
		.@mapName$ = strcharinfo(3);
		// Check map
		if(strcharinfo(3) == $maps$[.@mapIndex]){
			// Dead penalty
			if($bonusSpawnAmount[.@mapIndex] > 0){
				$bonusSpawnAmount[.@mapIndex] /= 2;
			}
			atcommand "@statsall -" + $deadStatusPenalty[.@mapIndex];
			atcommand "@traitall -" + $deadTraitPenalty[.@mapIndex];
			atcommand "@grade 0 -4";
			atcommand "@refine 0 -20";
		}
	}
	end;
	
OnInit:
	waitingroom "เล่นระดับ " + (atoi(strnpcinfo(2)) + 1),0;
	callsub(OnSpawn);
	end;
}

vr_pop3,172,168,3	duplicate(เริ่มเกม#0)	เริ่มเกม#1	10007
vr_pop3,168,173,3	duplicate(เริ่มเกม#0)	เริ่มเกม#2	10007
vr_pop3,162,174,3	duplicate(เริ่มเกม#0)	เริ่มเกม#3	10007
vr_pop3,155,173,3	duplicate(เริ่มเกม#0)	เริ่มเกม#4	10007
vr_pop3,148,171,3	duplicate(เริ่มเกม#0)	เริ่มเกม#5	10007
vr_pop3,141,169,3	duplicate(เริ่มเกม#0)	เริ่มเกม#6	10007
vr_pop3,140,160,3	duplicate(เริ่มเกม#0)	เริ่มเกม#7	10007
vr_pop3,149,156,3	duplicate(เริ่มเกม#0)	เริ่มเกม#8	10007
vr_pop3,158,155,3	duplicate(เริ่มเกม#0)	เริ่มเกม#9	10007
