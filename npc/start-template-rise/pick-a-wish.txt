-	script	PickAWish	-1,{
	if(!isSuitPicked)
	{
		end;
	}

	if(!isWishGranted)
	{
		mes "ท่านยังไม่เคยขอพร..";
		close;
	}
	
	.@index = atoi(strnpcinfo(2));
	
	if(maximumWishSet <= .@index)
	{
		mes "ไม่สามารถขอพร Set ที่ " + (.@index + 1) + " ได้ในขณะนี้..";
		close;
	}
	
	ClearAll();
	
	freeloop(1);
	
	// Sleeping Pill
	getitem SLEEPING_PILL,sleepingPill[.@index];
	
	// Normal Usable Item
	getitem 602,1;
	getitem 12622,1;
	getitem 12887,1;
	getitem 23023,1;
	
	// Status
	statusup2 bStr,statusStr[.@index];
	statusup2 bAgi,statusAgi[.@index];
	statusup2 bVit,statusVit[.@index];
	statusup2 bInt,statusInt[.@index];
	statusup2 bDex,statusDex[.@index];
	statusup2 bLuk,statusLuk[.@index];
	
	// Trait
	traitstatusup2 bPow,traitPow[.@index];
	traitstatusup2 bSta,traitSta[.@index];
	traitstatusup2 bWis,traitWis[.@index];
	traitstatusup2 bSpl,traitSpl[.@index];
	traitstatusup2 bCon,traitCon[.@index];
	traitstatusup2 bCrt,traitCrt[.@index];
	
	// Skill
	for(.@s = 0; .@s < skillAmount[.@index]; .@s++)
	{
		.@skillIndex = getd("skillIndex" + .@index + "[" + .@s + "]");
		bonus_script "skill " + $skillIds[.@skillIndex] + "," + $skillMaxLvs[.@skillIndex] + ";",86400,512;
	}
	
	// Equipment
	for(.@r = 0; .@r <= 20; .@r++)
	{
		.@itemId = getd("itemId" + .@index + "[" + .@r + "]");
		.@refine = getd("refine" + .@index + "[" + .@r + "]");
		.@card1 = getd("card" + .@index + "_" + .@r + "[0]");
		.@card2 = getd("card" + .@index + "_" + .@r + "[1]");
		.@card3 = getd("card" + .@index + "_" + .@r + "[2]");
		.@card4 = getd("card" + .@index + "_" + .@r + "[3]");
		.@enchantment1 = getd("enchantment" + .@index + "_" + .@r + "[0]");
		.@enchantment2 = getd("enchantment" + .@index + "_" + .@r + "[1]");
		.@enchantment3 = getd("enchantment" + .@index + "_" + .@r + "[2]");
		.@enchantment4 = getd("enchantment" + .@index + "_" + .@r + "[3]");
		.@slot1 = (.@card1 > 0) ? .@card1 : .@enchantment1;
		.@slot2 = (.@card2 > 0) ? .@card2 : .@enchantment2;
		.@slot3 = (.@card3 > 0) ? .@card3 : .@enchantment3;
		.@slot4 = (.@card4 > 0) ? .@card4 : .@enchantment4;
		.@grade = getd("grade" + .@index + "[" + .@r + "]");
		setarray .@randomOptionIds[0],getd("randomOption" + .@index + "_" + .@r + "[0]"),getd("randomOption" + .@index + "_" + .@r + "[1]"),getd("randomOption" + .@index + "_" + .@r + "[2]"),getd("randomOption" + .@index + "_" + .@r + "[3]"),getd("randomOption" + .@index + "_" + .@r + "[4]");
		setarray .@randomOptionValues[0],1,1,1,1,1;
		setarray .@randomOptionParams[0],0,0,0,0,0;
		if(.@itemId > 0)
		{
			getitem4 .@itemId,1,1,.@refine,0,.@slot1,.@slot2,.@slot3,.@slot4,.@grade,.@randomOptionIds,.@randomOptionValues,.@randomOptionParams;
			equip .@itemId;
		}
	}
	
	// Run equip again to prevent Accessory not equipped
	for(.@r = 0; .@r <= 20; .@r++)
	{
		.@itemId = getd("itemId" + .@index + "[" + .@r + "]");
		if(.@itemId > 0)
		{
			equip .@itemId;
		}
	}
	
	freeloop(0);
	
	FullHeal();
	
	dispbottom "รับพร Set ที่ " + (.@index + 1) + " เรียบร้อยแล้ว!",0x33ffa5;
	
	end;
	
OnInit:
	waitingroom "รับ" + strnpcinfo(1),0;
	end;
}

moc_akhet,178,188,5	duplicate(PickAWish)	พร Set ที่ 1#0	10172
moc_akhet,178,194,5	duplicate(PickAWish)	พร Set ที่ 2#1	10172
moc_akhet,178,200,5	duplicate(PickAWish)	พร Set ที่ 3#2	10172
moc_akhet,178,206,5	duplicate(PickAWish)	พร Set ที่ 4#3	10172
moc_akhet,178,212,5	duplicate(PickAWish)	พร Set ที่ 5#4	10172
moc_akhet,178,218,5	duplicate(PickAWish)	พร Set ที่ 6#5	10172
moc_akhet,201,188,4	duplicate(PickAWish)	พร Set ที่ 7#6	10172
moc_akhet,201,194,4	duplicate(PickAWish)	พร Set ที่ 8#7	10172
moc_akhet,201,200,4	duplicate(PickAWish)	พร Set ที่ 9#8	10172
moc_akhet,201,206,4	duplicate(PickAWish)	พร Set ที่ 10#9	10172
moc_akhet,201,212,4	duplicate(PickAWish)	พร Set ที่ 11#10	10172
moc_akhet,201,218,4	duplicate(PickAWish)	พร Set ที่ 12#11	10172

moc_akhet,190,195,4	script	ขอพร	10545,{
	if(isWishGenerated){
		mes "ไม่สามารถขอพร Set ใหม่ได้ จนกว่าจะเกิดเหตุการณ์ดังนี้";
		mes "^17b217 ชั้นความฝันเพิ่มขึ้น^000000";
		mes "^ff3333 ชั้นความฝันลดลง^000000";
		close;
	}
	.@dreamLevelIndex = GetDreamLevelIndex();
	.@dreamLevelWording = (.@dreamLevelIndex + 1);
	mes "ท่านต้องการที่จะขอพร Set ใหม่ ใช่หรือไม่?";
	mes "^ff3333*บาง Skill จะยังมีเงื่อนไขเฉพาะอยู่^000000";
	mes "^17b217*แต่ส่วนใหญ่แล้ว จะกดใช้ได้เลย^000000^ff3333 (การใช้ HP, SP และ AP ยังมีอยู่)^000000";
	next;
	menu "ยกเลิก",-,"ยกเลิก",-,"ยกเลิก",-,"ขอพร Set ใหม่ (ระดับพร " + .@dreamLevelWording + ")",OnGenerateWish;
	close;
	
OnGenerateWish:
	maximumWishSet = $maximumWishSet[.@dreamLevelIndex];
	freeloop(1);
	for(.@i = 0; .@i < maximumWishSet; .@i++)
	{
		// Sleeping Pill
		sleepingPill[.@i] = rand(0,$maximumPill[.@dreamLevelIndex]);
		
		// Status
		.@minimumStatus = $minimumStatus[.@dreamLevelIndex];
		statusStr[.@i] = rand(.@minimumStatus,$maximumStatus);
		statusAgi[.@i] = rand(.@minimumStatus,$maximumStatus);
		statusVit[.@i] = rand(.@minimumStatus,$maximumStatus);
		statusInt[.@i] = rand(.@minimumStatus,$maximumStatus);
		statusDex[.@i] = rand(.@minimumStatus,$maximumStatus);
		statusLuk[.@i] = rand(.@minimumStatus,$maximumStatus);
		
		// Trait
		.@minimumTrait = $minimumTrait[.@dreamLevelIndex];
		traitPow[.@i] = rand(.@minimumTrait,$maximumTrait);
		traitSta[.@i] = rand(.@minimumTrait,$maximumTrait);
		traitWis[.@i] = rand(.@minimumTrait,$maximumTrait);
		traitSpl[.@i] = rand(.@minimumTrait,$maximumTrait);
		traitCon[.@i] = rand(.@minimumTrait,$maximumTrait);
		traitCrt[.@i] = rand(.@minimumTrait,$maximumTrait);
		
		// Skill
		deletearray getd("skillIndex" + .@i + "[0]"),getarraysize(getd("skillIndex" + .@i));
		skillAmount[.@i] = rand($minimumSkill[.@dreamLevelIndex],$maximumSkill[.@dreamLevelIndex]);
		.@maximumSkillId = getarraysize($skillIds);
		for(.@s = 0; .@s < skillAmount[.@i]; .@s++)
		{
			.@randomSkillIndex = rand(.@maximumSkillId);
			while(inarray(getd("skillIndex" + .@i),.@randomSkillIndex) >= 0)
			{
				.@randomSkillIndex = rand(.@maximumSkillId);
			}
			setarray getd("skillIndex" + .@i + "[" + .@s + "]"),.@randomSkillIndex;
		}
		
		// Equipment
		deletearray getd("itemId" + .@i + "[0]"),getarraysize(getd("itemId" + .@i));
		deletearray getd("banLocation" + .@i + "[0]"),getarraysize(getd("banLocation" + .@i));
		for(.@r = 20; .@r >= 0; .@r--)
		{
			deletearray .@location$[0],getarraysize(.@location$);
			
			if(getd("banLocation" + .@i + "[" + .@r + "]"))
			{
				continue;
			}
			
			if(.@r == 14) // EQI_AMMO
			{
				continue;
			}
			
			if(.@r == 0) // EQI_ACC_L
			{
				setarray .@location$[0],"A","LA";
			}
			else if(.@r == 1) // EQI_ACC_R
			{
				setarray .@location$[0],"A","RA";
			}
			else if(.@r == 2) // EQI_SHOES
			{
				setarray .@location$[0],"SHOES";
			}
			else if(.@r == 3) // EQI_GARMENT
			{
				setarray .@location$[0],"GARMENT";
			}
			else if(.@r == 4) // EQI_HEAD_LOW
			{
				//setarray .@location$[0],"LOW","LOWMIDTOP","LOWMID","LOWTOP","TOPMIDLOW","MIDLOW","TOPLOW";
				setarray .@location$[0],"LOW"; // Loop 20 to 0
				// 75% Chance to appear this location
				if(rand(0,4) < 4)
				{
					deletearray .@location$[0],getarraysize(.@location$);
					setarray .@location$[0],"LOW";
				}
			}
			else if(.@r == 5) // EQI_HEAD_MID
			{
				//setarray .@location$[0],"MID","MIDTOP","LOWMIDTOP","LOWMID","TOPMID","TOPMIDLOW","MIDLOW";
				setarray .@location$[0],"MID","LOWMID","MIDLOW"; // Loop 20 to 0
				// 75% Chance to appear this location
				if(rand(0,4) < 4)
				{
					deletearray .@location$[0],getarraysize(.@location$);
					setarray .@location$[0],"MID";
				}
			}
			else if(.@r == 6) // EQI_HEAD_TOP
			{
				setarray .@location$[0],"TOP","MIDTOP","LOWMIDTOP","LOWTOP","TOPMID","TOPMIDLOW","TOPLOW";
				// 75% Chance to appear this location
				if(rand(0,4) < 4)
				{
					deletearray .@location$[0],getarraysize(.@location$);
					setarray .@location$[0],"TOP";
				}
			}
			else if(.@r == 7) // EQI_ARMOR
			{
				setarray .@location$[0],"ARMOR";
			}
			else if(.@r == 8) // EQI_HAND_L
			{
				if(getiteminfo(getd("itemId" + .@i + "[9]"),ITEMINFO_LOCATIONS) & 32)
				{
					continue;
				}
				setarray .@location$[0],"SHIELD";
			}
			else if(.@r == 9) // EQI_HAND_R
			{
				setarray .@location$[0],"1hSword","2hSword","Dagger","Katar","1hAxe","2hAxe","Huuma","Mace","1hSpear","2hSpear","2hStaff","Book","Staff","Bow","Knuckle","Musical","Whip","Revolver","Rifle","Shotgun","Gatling","Grenade";
			}
			else if(.@r == 10) // EQI_COSTUME_HEAD_TOP
			{
				//setarray .@location$[0],"CTOP","CTOPCMID","CTOPCMIDCLOW","CTOPCLOW";
				setarray .@location$[0],"CTOP"; // Loop 20 to 0
				// 75% Chance to appear this location
				if(rand(0,4) < 4)
				{
					deletearray .@location$[0],getarraysize(.@location$);
					setarray .@location$[0],"CTOP";
				}
			}
			else if(.@r == 11) // EQI_COSTUME_HEAD_MID
			{
				//setarray .@location$[0],"CMID","CMIDCLOW","CTOPCMID","CTOPCMIDCLOW";
				setarray .@location$[0],"CMID","CTOPCMID"; // Loop 20 to 0
				// 75% Chance to appear this location
				if(rand(0,4) < 4)
				{
					deletearray .@location$[0],getarraysize(.@location$);
					setarray .@location$[0],"CMID";
				}
			}
			else if(.@r == 12) // EQI_COSTUME_HEAD_LOW
			{
				setarray .@location$[0],"CLOW","CMIDCLOW","CTOPCMIDCLOW","CTOPCLOW";
				// 75% Chance to appear this location
				if(rand(0,4) < 4)
				{
					deletearray .@location$[0],getarraysize(.@location$);
					setarray .@location$[0],"CLOW";
				}
			}
			else if(.@r == 13) // EQI_COSTUME_GARMENT
			{
				setarray .@location$[0],"CGARMENT";
			}
			else if(.@r == 15) // EQI_SHADOW_ARMOR
			{
				setarray .@location$[0],"SARMOR";
			}
			else if(.@r == 16) // EQI_SHADOW_WEAPON
			{
				setarray .@location$[0],"SWEAPON";
			}
			else if(.@r == 17) // EQI_SHADOW_SHIELD
			{
				setarray .@location$[0],"SSHIELD";
			}
			else if(.@r == 18) // EQI_SHADOW_SHOES
			{
				setarray .@location$[0],"SSHOES";
			}
			else if(.@r == 19) // EQI_SHADOW_ACC_R
			{
				setarray .@location$[0],"SRA";
			}
			else if(.@r == 20) // EQI_SHADOW_ACC_L
			{
				setarray .@location$[0],"SLA";
			}
			.@variableArraySize = getarraysize(.@location$);
			if(.@variableArraySize > 1)
			{
				.@locationVariableName$ = "$" + ((.@r == 9) ? "Weapon_" : "") + .@location$[rand(.@variableArraySize)];
			}
			else
			{
				.@locationVariableName$ = "$" + ((.@r == 9) ? "Weapon_" : "") + .@location$[0];
			}
			
			// Ban Location
			if(.@locationVariableName$ == "$CMIDCLOW")
			{
				set getd("banLocation" + .@i + "[11]"),1;
				set getd("banLocation" + .@i + "[12]"),1;
			}
			else if(.@locationVariableName$ == "$CTOPCMIDCLOW")
			{
				set getd("banLocation" + .@i + "[10]"),1;
				set getd("banLocation" + .@i + "[11]"),1;
				set getd("banLocation" + .@i + "[12]"),1;
			}
			else if(.@locationVariableName$ == "$CTOPCLOW")
			{
				set getd("banLocation" + .@i + "[10]"),1;
				set getd("banLocation" + .@i + "[12]"),1;
			}
			else if(.@locationVariableName$ == "$CTOPCMID")
			{
				set getd("banLocation" + .@i + "[10]"),1;
				set getd("banLocation" + .@i + "[11]"),1;
			}
			else if((.@locationVariableName$ == "$MIDTOP") || (.@locationVariableName$ == "$TOPMID"))
			{
				set getd("banLocation" + .@i + "[5]"),1;
				set getd("banLocation" + .@i + "[6]"),1;
			}
			else if((.@locationVariableName$ == "$LOWMIDTOP") || (.@locationVariableName$ == "$TOPMIDLOW"))
			{
				set getd("banLocation" + .@i + "[4]"),1;
				set getd("banLocation" + .@i + "[5]"),1;
				set getd("banLocation" + .@i + "[6]"),1;
			}
			else if((.@locationVariableName$ == "$LOWTOP") || (.@locationVariableName$ == "$TOPLOW"))
			{
				set getd("banLocation" + .@i + "[4]"),1;
				set getd("banLocation" + .@i + "[6]"),1;
			}
			else if((.@locationVariableName$ == "$LOWMID") || (.@locationVariableName$ == "$MIDLOW"))
			{
				set getd("banLocation" + .@i + "[4]"),1;
				set getd("banLocation" + .@i + "[5]"),1;
			}
			
			.@itemArraySize = getarraysize(getd(.@locationVariableName$));
			if(.@itemArraySize > 1)
			{
				set getd("itemId" + .@i + "[" +.@r + "]"),getd(.@locationVariableName$ + "[" + rand(.@itemArraySize) + "]");
			}
			else
			{
				set getd("itemId" + .@i + "[" +.@r + "]"),getd(.@locationVariableName$ + "[0]");
			}
			// Refine
			.@minimumRefine = $minimumRefine[.@dreamLevelIndex];
			.@maximumRefine = $maximumRefine[.@dreamLevelIndex];
			set getd("refine" + .@i + "[" + .@r + "]"),rand(.@minimumRefine,.@maximumRefine);
			// Grade
			.@minimumGrade = $minimumGrade[.@dreamLevelIndex];
			.@maximumGrade = $maximumGrade[.@dreamLevelIndex];
			set getd("grade" + .@i + "[" + .@r + "]"),rand(.@minimumGrade,.@maximumGrade);
			// Random Option
			.@minimumRandomOption = $minimumRandomOption[.@dreamLevelIndex];
			.@maximumRandomOption = $maximumRandomOption[.@dreamLevelIndex];
			.@randomOptionAmount = rand(.@minimumRandomOption,.@maximumRandomOption);
			set getd("randomOption" + .@i + "_" + .@r + "[0]"),(.@randomOptionAmount >= 1) ? GetRandomOptionId() : 0;
			set getd("randomOption" + .@i + "_" + .@r + "[1]"),(.@randomOptionAmount >= 2) ? GetRandomOptionId() : 0;
			set getd("randomOption" + .@i + "_" + .@r + "[2]"),(.@randomOptionAmount >= 3) ? GetRandomOptionId() : 0;
			set getd("randomOption" + .@i + "_" + .@r + "[3]"),(.@randomOptionAmount >= 4) ? GetRandomOptionId() : 0;
			set getd("randomOption" + .@i + "_" + .@r + "[4]"),(.@randomOptionAmount >= 5) ? GetRandomOptionId() : 0;
			// Slot
			.@itemSlot = getitemslots(getd("itemId" + .@i + "[" +.@r + "]"));
			// Card
			.@cardAmount = getarraysize($card2Ids);
			.@cardRate = $cardRate[.@dreamLevelIndex];
			set getd("card" + .@i + "_" + .@r + "[0]"),((.@itemSlot >= 1) && (rand(100) < .@cardRate)) ? $card2Ids[rand(.@cardAmount)] : 0;
			set getd("card" + .@i + "_" + .@r + "[1]"),((.@itemSlot >= 2) && (rand(100) < .@cardRate)) ? $card2Ids[rand(.@cardAmount)] : 0;
			set getd("card" + .@i + "_" + .@r + "[2]"),((.@itemSlot >= 3) && (rand(100) < .@cardRate)) ? $card2Ids[rand(.@cardAmount)] : 0;
			set getd("card" + .@i + "_" + .@r + "[3]"),((.@itemSlot >= 4) && (rand(100) < .@cardRate)) ? $card2Ids[rand(.@cardAmount)] : 0;
			// Enchantment
			.@enchantmentAmount = getarraysize($enchant2Ids);
			.@enchantmentRate = $enchantmentRate[.@dreamLevelIndex];
			set getd("enchantment" + .@i + "_" + .@r + "[0]"),((.@itemSlot < 1) && (rand(100) < .@enchantmentRate)) ? $enchant2Ids[rand(.@enchantmentAmount)] : 0;
			set getd("enchantment" + .@i + "_" + .@r + "[1]"),((.@itemSlot < 2) && (rand(100) < .@enchantmentRate)) ? $enchant2Ids[rand(.@enchantmentAmount)] : 0;
			set getd("enchantment" + .@i + "_" + .@r + "[2]"),((.@itemSlot < 3) && (rand(100) < .@enchantmentRate)) ? $enchant2Ids[rand(.@enchantmentAmount)] : 0;
			set getd("enchantment" + .@i + "_" + .@r + "[3]"),((.@itemSlot < 4) && (rand(100) < .@enchantmentRate)) ? $enchant2Ids[rand(.@enchantmentAmount)] : 0;
		}
	}
	freeloop(0);
	isWishGranted = 1;
	mes "ขอพรใหม่แล้วทั้งหมด " + maximumWishSet +" Set";
	isWishGenerated = 1;
	close;
	
OnInit:
	waitingroom "ขอพร Set ใหม่",0;
	end;
}

moc_akhet,190,205,4	script	ของใช้ตาม Skill	10249,{
	Zeny = 10000;
	
	menu
	"^f1820cFalcon^000000",OnFalcon
	,"^f1820cMado Gear^000000",OnMadoGear
	,"^f1820cDragon^000000",OnDragon
	,"^b81abaเคลือบธาตุ^000000",OnShopElement
	,"^0cb6f1Item ในการกด Skill (Set ที่ 1)^000000",OnSkillItem1
	,"^0cb6f1Item ในการกด Skill (Set ที่ 2)^000000",OnSkillItem2
	,"^4f5f5fArrow^000000",OnAmmoArrow
	,"^4f5f5fBullet^000000",OnAmmoBullet
	,"^4f5f5fShuriken^000000",OnAmmoShuriken
	,"^4f5f5fKunai^000000",OnAmmoKunai
	,"^4f5f5fThrow Weapon^000000",OnAmmoThrowWeapon
	,"^4f5f5fCannon Ball^000000",OnAmmoCannonBall
	;
	
OnShopElement: callshop "ShopElement",1; end;
OnSkillItem1: callshop "ShopSkillItem1",1; end;
OnSkillItem2: callshop "ShopSkillItem2",1; end;
OnAmmoArrow: callshop "ShopArrow",1; end;
OnAmmoBullet: callshop "ShopBullet",1; end;
OnAmmoShuriken: callshop "ShopShuriken",1; end;
OnAmmoKunai: callshop "ShopKunai",1; end;
OnAmmoThrowWeapon: callshop "ShopThrowWeapon",1; end;
OnAmmoCannonBall: callshop "ShopCannonBall",1; end;
	
OnFalcon:
	if(checkfalcon())
	{
		end;
	}
	if(!getskilllv("HT_FALCON"))
	{
		message strcharinfo(0),"ต้องมี Skill Falconry Mastery เสียก่อน";
		end;
	}
	setfalcon;
	end;
	
OnMadoGear:
	if(checkmadogear())
	{
		end;
	}
	if((eaclass()&EAJ_THIRDMASK) != EAJ_MECHANIC)
	{
		message strcharinfo(0),"เฉพาะชุด Meister เท่านั้น";
		end;
	}
	if(!getskilllv("NC_MADOLICENCE"))
	{
		message strcharinfo(0),"ต้องมี Skill Mado License เสียก่อน";
		end;
	}
	setmadogear;
	end;
	
OnDragon:
	if(checkdragon())
	{
		end;
	}
	if((eaclass()&EAJ_THIRDMASK) != EAJ_RUNE_KNIGHT)
	{
		message strcharinfo(0),"เฉพาะชุด Dragon Knight เท่านั้น";
		end;
	}
	if(!getskilllv("RK_DRAGONTRAINING"))
	{
		message strcharinfo(0),"ต้องมี Skill Dragon Training เสียก่อน";
		end;
	}
	setdragon;
	end;
	
OnInit:
	waitingroom strnpcinfo(0),0;
	end;
}

-	shop	ShopElement	-1,12020:1,12114:1,12115:1,12116:1,12117:1,12118:1,12119:1,12120:1,12121:1,12406:1,14539:1,14540:1
-	shop	ShopSkillItem1	-1,717:1,716:1,1065:1,7049:1,7134:1,7135:1,7136:1,7137:1,7138:1,7139:1,6360:1,6361:1,6362:1,6363:1,715:1,1025:1,921:1,905:1,7524:1,7521:1,7522:1,7523:1,6128:1,7940:1,6124:1,6145:1,6146:1,6147:1,998:1,999:1,7054:1,6186:1,6122:1,6123:1,6120:1,6121:1,6144:1,990:1,994:1,991:1,995:1,992:1,996:1,993:1,997:1,6210:1,6211:1,6212:1,6216:1,6213:1,6214:1,6215:1,6217:1,6377:1,7663:1,7664:1,7665:1,25187:1
-	shop	ShopSkillItem2	-1,6493:1,6512:1,6513:1,6514:1,6515:1,970:1,1000352:1,1000289:1,1000290:1,1000311:1,1000280:1,1000279:1,1000277:1,1000278:1,1000276:1,1000281:1,1000293:1,1000295:1,1000291:1,1000292:1,1000294:1,1000564:1,1000566:1,1000567:1,1000569:1,1000568:1,1000565:1,12717:1,12722:1,12720:1,12718:1,12724:1,12723:1,12721:1,12719:1,100065:1,100066:1,100067:1,100068:1,100069:1,100070:1,100071:1,100072:1,100073:1,100074:1,6284:1,6285:1,11022:1,11023:1,11024:1,6248:1,6250:1,6251:1,6253:1,6255:1,6258:1,6261:1,6262:1,1000275:1,7433:1,12731:1,12728:1,12732:1,12733:1,12729:1,12730:1,12726:1,12725:1,12727:1,22540:1,23277:1,100158:1,1000563:1

-	shop	ShopArrow	-1,1750:1,1751:1,1752:1,1753:1,1754:1,1755:1,1756:1,1757:1,1758:1,1759:1,1760:1,1761:1,1762:1,1763:1,1764:1,1765:1,1766:1,1767:1,1768:1,1769:1,1770:1,1772:1,1773:1,1774:1,1775:1,1776:1,1200000:1
-	shop	ShopBullet	-1,13200:1,13201:1,13202:1,13208:1,13210:1,13211:1,13212:1,13213:1,13214:1,13215:1,13216:1,13217:1,13218:1,13219:1,13220:1,13221:1,13222:1,13228:1,13229:1,13230:1,13231:1,13232:1
-	shop	ShopShuriken	-1,13250:1,13251:1,13252:1,13253:1,13254:1,13291:1,13295:1
-	shop	ShopKunai	-1,13255:1,13256:1,13257:1,13258:1,13259:1,13292:1,13293:1,13294:1,1220001:1,1220002:1,1220003:1,1220004:1
-	shop	ShopThrowWeapon	-1,13260:1,13261:1,13262:1,13263:1,13264:1,13265:1,13266:1,13267:1,13268:1,13269:1,13270:1,13271:1,13272:1,13273:1,13274:1,13275:1,13276:1,13277:1,13278:1,13279:1,13280:1,13281:1,13282:1,13283:1,13284:1,13285:1,13286:1,13287:1,13288:1,13289:1,13290:1
-	shop	ShopCannonBall	-1,18000:1,18001:1,18002:1,18003:1,18004:1,18005:1,18006:1,18007:1,18008:1,18009:1,1210000:1,1220000:1
