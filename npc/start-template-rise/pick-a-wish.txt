-	script	PickAWish	-1,{
	if(!isSuitPicked)
	{
		end;
	}

	if(!isWishGranted)
	{
		mes "ท่านยังไม่เคยขอพร..";
		close;
	}
	
	.@index = atoi(strnpcinfo(2));
	
	if(maximumWishSet <= .@index)
	{
		mes "ไม่สามารถขอพร Set ที่ " + (.@index + 1) + " ได้ในขณะนี้..";
		close;
	}
	
	ClearAll();
	
	freeloop(1);
	
	// Status
	statusup2 bStr,statusStr[.@index];
	statusup2 bAgi,statusAgi[.@index];
	statusup2 bVit,statusVit[.@index];
	statusup2 bInt,statusInt[.@index];
	statusup2 bDex,statusDex[.@index];
	statusup2 bLuk,statusLuk[.@index];
	
	// Trait
	traitstatusup2 bPow,traitPow[.@index];
	traitstatusup2 bSta,traitSta[.@index];
	traitstatusup2 bWis,traitWis[.@index];
	traitstatusup2 bSpl,traitSpl[.@index];
	traitstatusup2 bCon,traitCon[.@index];
	traitstatusup2 bCrt,traitCrt[.@index];
	
	// Skill
	
	// Equipment
	for(.@r = 0; .@r < 20; .@r++)
	{
		.@itemId = getd("itemId" + .@index + "[" + .@r + "]");
		if(.@itemId > 0){
			getitem .@itemId,1;
			equip .@itemId;
		}
		// Refine
		// Grade
		// Random Option
		// Slot
		// Card
		// Enchantment
	}
	
	freeloop(0);
	
	FullHeal();
	
	message strcharinfo(0),"รับพร Set ที่ " + (.@index + 1) + " เรียบร้อยแล้ว!";
	end;
	
OnInit:
	waitingroom strnpcinfo(1),0;
	end;
}

moc_akhet,178,188,5	duplicate(PickAWish)	พร Set ที่ 1#0	10172
moc_akhet,178,194,5	duplicate(PickAWish)	พร Set ที่ 2#1	10172
moc_akhet,178,200,5	duplicate(PickAWish)	พร Set ที่ 3#2	10172
moc_akhet,178,206,5	duplicate(PickAWish)	พร Set ที่ 4#3	10172
moc_akhet,178,212,5	duplicate(PickAWish)	พร Set ที่ 5#4	10172
moc_akhet,178,218,5	duplicate(PickAWish)	พร Set ที่ 6#5	10172
moc_akhet,201,188,4	duplicate(PickAWish)	พร Set ที่ 7#6	10172
moc_akhet,201,194,4	duplicate(PickAWish)	พร Set ที่ 8#7	10172
moc_akhet,201,200,4	duplicate(PickAWish)	พร Set ที่ 9#8	10172
moc_akhet,201,206,4	duplicate(PickAWish)	พร Set ที่ 10#9	10172
moc_akhet,201,212,4	duplicate(PickAWish)	พร Set ที่ 11#10	10172
moc_akhet,201,218,4	duplicate(PickAWish)	พร Set ที่ 12#11	10172

moc_akhet,190,195,4	script	ขอพร	10545,{
	.@dreamLevelIndex = GetDreamLevelIndex();
	.@dreamLevelWording = (.@dreamLevelIndex + 1);
	mes "ท่านต้องการที่จะขอพร Set ใหม่ ใช่หรือไม่?";
	next;
	menu "ยกเลิก",-,"ยกเลิก",-,"ยกเลิก",-,"ขอพร Set ใหม่ (ระดับพร " + .@dreamLevelWording + ")",OnGenerateWish;
	close;
	
OnGenerateWish:
	maximumWishSet = $maximumWishSet[.@dreamLevelIndex];
	freeloop(1);
	for(.@i = 0; .@i < maximumWishSet; .@i++)
	{
		// Status
		.@minimumStatus = $minimumStatus[.@dreamLevelIndex];
		statusStr[.@i] = rand(.@minimumStatus,$maximumStatus);
		statusAgi[.@i] = rand(.@minimumStatus,$maximumStatus);
		statusVit[.@i] = rand(.@minimumStatus,$maximumStatus);
		statusInt[.@i] = rand(.@minimumStatus,$maximumStatus);
		statusDex[.@i] = rand(.@minimumStatus,$maximumStatus);
		statusLuk[.@i] = rand(.@minimumStatus,$maximumStatus);
		
		// Trait
		.@minimumTrait = $minimumTrait[.@dreamLevelIndex];
		traitPow[.@i] = rand(.@minimumTrait,$maximumTrait);
		traitSta[.@i] = rand(.@minimumTrait,$maximumTrait);
		traitWis[.@i] = rand(.@minimumTrait,$maximumTrait);
		traitSpl[.@i] = rand(.@minimumTrait,$maximumTrait);
		traitCon[.@i] = rand(.@minimumTrait,$maximumTrait);
		traitCrt[.@i] = rand(.@minimumTrait,$maximumTrait);
		
		// Skill
		
		// Equipment
		deletearray getd("itemId" + .@i + "[0]"),getarraysize(getd("itemId" + .@i));
		for(.@r = 20; .@r >= 0; .@r--)
		{
			deletearray .@location$[0],getarraysize(.@location$);

			if(.@r == 14) // EQI_AMMO
			{
				continue;
			}
			
			if(.@r == 0) // EQI_ACC_L
			{
				setarray .@location$[0],"A","LA";
			}
			else if(.@r == 1) // EQI_ACC_R
			{
				setarray .@location$[0],"A","RA";
			}
			else if(.@r == 2) // EQI_SHOES
			{
				setarray .@location$[0],"SHOES";
			}
			else if(.@r == 3) // EQI_GARMENT
			{
				setarray .@location$[0],"GARMENT";
			}
			else if(.@r == 4) // EQI_HEAD_LOW
			{
				if(((getd("itemId" + .@i + "[5]") != 0) && ((getiteminfo(getd("itemId" + .@i + "[5]"),ITEMINFO_LOCATIONS) & 1) != 0)) || ((getd("itemId" + .@i + "[6]") != 0) && ((getiteminfo(getd("itemId" + .@i + "[6]"),ITEMINFO_LOCATIONS) & 1) != 0)))
				{
					continue;
				}
				setarray .@location$[0],"LOW","LOWMIDTOP","LOWMID","LOWTOP","TOPMIDLOW","MIDLOW","TOPLOW";
				// 50% Chance to appear this location
				if(rand(0,1) == 0)
				{
					deletearray .@location$[0],getarraysize(.@location$);
					setarray .@location$[0],"LOW";
				}
			}
			else if(.@r == 5) // EQI_HEAD_MID
			{
				if(((getd("itemId" + .@i + "[4]") != 0) && ((getiteminfo(getd("itemId" + .@i + "[4]"),ITEMINFO_LOCATIONS) & 512) != 0)) || ((getd("itemId" + .@i + "[6]") != 0) && ((getiteminfo(getd("itemId" + .@i + "[6]"),ITEMINFO_LOCATIONS) & 512) != 0)))
				{
					continue;
				}
				setarray .@location$[0],"MID","MIDTOP","LOWMIDTOP","LOWMID","TOPMID","TOPMIDLOW","MIDLOW";
				// 50% Chance to appear this location
				if(rand(0,1) == 0)
				{
					deletearray .@location$[0],getarraysize(.@location$);
					setarray .@location$[0],"MID";
				}
			}
			else if(.@r == 6) // EQI_HEAD_TOP
			{
				if(((getd("itemId" + .@i + "[4]") != 0) && ((getiteminfo(getd("itemId" + .@i + "[4]"),ITEMINFO_LOCATIONS) & 256) != 0)) || ((getd("itemId" + .@i + "[5]") != 0) && ((getiteminfo(getd("itemId" + .@i + "[5]"),ITEMINFO_LOCATIONS) & 256) != 0)))
				{
					continue;
				}
				setarray .@location$[0],"TOP","MIDTOP","LOWMIDTOP","LOWTOP","TOPMID","TOPMIDLOW","TOPLOW";
				// 50% Chance to appear this location
				if(rand(0,1) == 0)
				{
					deletearray .@location$[0],getarraysize(.@location$);
					setarray .@location$[0],"TOP";
				}
			}
			else if(.@r == 7) // EQI_ARMOR
			{
				setarray .@location$[0],"ARMOR";
			}
			else if(.@r == 8) // EQI_HAND_L
			{
				if((getiteminfo(itemId[9],ITEMINFO_LOCATIONS) & 32) != 0)
				{
					continue;
				}
				setarray .@location$[0],"SHIELD";
			}
			else if(.@r == 9) // EQI_HAND_R
			{
				setarray .@location$[0],"1hSword","2hSword","Dagger","Katar","1hAxe","2hAxe","Huuma","Mace","1hSpear","2hSpear","2hStaff","Book","Staff","Bow","Knuckle","Musical","Whip","Revolver","Rifle","Shotgun","Gatling","Grenade";
			}
			else if(.@r == 10) // EQI_COSTUME_HEAD_TOP
			{
				if(((getd("itemId" + .@i + "[11]") != 0) && ((getiteminfo(getd("itemId" + .@i + "[11]"),ITEMINFO_LOCATIONS) & 1024) != 0)) || ((getd("itemId" + .@i + "[12]") != 0) && ((getiteminfo(getd("itemId" + .@i + "[12]"),ITEMINFO_LOCATIONS) & 1024) != 0)))
				{
					continue;
				}
				setarray .@location$[0],"CTOP","CTOPCMID","CTOPCMIDCLOW","CTOPCLOW";
				// 50% Chance to appear this location
				if(rand(0,1) == 0)
				{
					deletearray .@location$[0],getarraysize(.@location$);
					setarray .@location$[0],"CTOP";
				}
			}
			else if(.@r == 11) // EQI_COSTUME_HEAD_MID
			{
				if(((getd("itemId" + .@i + "[10]") != 0) && ((getiteminfo(getd("itemId" + .@i + "[10]"),ITEMINFO_LOCATIONS) & 2048) != 0)) || ((getd("itemId" + .@i + "[12]") != 0) && ((getiteminfo(getd("itemId" + .@i + "[12]"),ITEMINFO_LOCATIONS) & 2048) != 0)))
				{
					continue;
				}
				setarray .@location$[0],"CMID","CMIDCLOW","CTOPCMID","CTOPCMIDCLOW";
				// 50% Chance to appear this location
				if(rand(0,1) == 0)
				{
					deletearray .@location$[0],getarraysize(.@location$);
					setarray .@location$[0],"CMID";
				}
			}
			else if(.@r == 12) // EQI_COSTUME_HEAD_LOW
			{
				if(((getd("itemId" + .@i + "[10]") != 0) && ((getiteminfo(getd("itemId" + .@i + "[10]"),ITEMINFO_LOCATIONS) & 4096) != 0)) || ((getd("itemId" + .@i + "[11]") != 0) && ((getiteminfo(getd("itemId" + .@i + "[11]"),ITEMINFO_LOCATIONS) & 4096) != 0)))
				{
					continue;
				}
				setarray .@location$[0],"CLOW","CMIDCLOW","CTOPCMIDCLOW","CTOPCLOW";
				// 50% Chance to appear this location
				if(rand(0,1) == 0)
				{
					deletearray .@location$[0],getarraysize(.@location$);
					setarray .@location$[0],"CLOW";
				}
			}
			else if(.@r == 13) // EQI_COSTUME_GARMENT
			{
				setarray .@location$[0],"CGARMENT";
			}
			else if(.@r == 15) // EQI_SHADOW_ARMOR
			{
				setarray .@location$[0],"SARMOR";
			}
			else if(.@r == 16) // EQI_SHADOW_WEAPON
			{
				setarray .@location$[0],"SWEAPON";
			}
			else if(.@r == 17) // EQI_SHADOW_SHIELD
			{
				setarray .@location$[0],"SSHIELD";
			}
			else if(.@r == 18) // EQI_SHADOW_SHOES
			{
				setarray .@location$[0],"SSHOES";
			}
			else if(.@r == 19) // EQI_SHADOW_ACC_R
			{
				setarray .@location$[0],"SRA";
			}
			else if(.@r == 20) // EQI_SHADOW_ACC_L
			{
				setarray .@location$[0],"SLA";
			}
			.@variableArraySize = getarraysize(.@location$);
			if(.@variableArraySize > 1)
			{
				.@locationVariableName$ = "$" + ((.@r == 9) ? "Weapon_" : "") + .@location$[rand(.@variableArraySize)];
			}
			else
			{
				.@locationVariableName$ = "$" + ((.@r == 9) ? "Weapon_" : "") + .@location$[0];
			}
			set getd("itemId" + .@i + "[" +.@r + "]"),getd(.@locationVariableName$ + "[" + rand(getarraysize(getd(.@locationVariableName$))) + "]");
			// Refine
			.@minimumRefine = $minimumRefine[.@dreamLevelIndex];
			.@maximumRefine = $maximumRefine[.@dreamLevelIndex];
			set getd("refine" + .@r),rand(.@minimumRefine,.@maximumRefine);
			// Grade
			.@minimumGrade = $minimumGrade[.@dreamLevelIndex];
			.@maximumGrade = $maximumGrade[.@dreamLevelIndex];
			set getd("grade" + .@r),rand(.@minimumGrade,.@maximumGrade);
			// Random Option
			.@minimumRandomOption = $minimumRandomOption[.@dreamLevelIndex];
			.@maximumRandomOption = $maximumRandomOption[.@dreamLevelIndex];
			.@randomOptionAmount = rand(.@minimumRandomOption,.@maximumRandomOption);
			set getd("randomOption" + .@r + "[0]"),(.@randomOptionAmount >= 1) ? GetRandomOptionId() : 0;
			set getd("randomOption" + .@r + "[1]"),(.@randomOptionAmount >= 2) ? GetRandomOptionId() : 0;
			set getd("randomOption" + .@r + "[2]"),(.@randomOptionAmount >= 3) ? GetRandomOptionId() : 0;
			set getd("randomOption" + .@r + "[3]"),(.@randomOptionAmount >= 4) ? GetRandomOptionId() : 0;
			set getd("randomOption" + .@r + "[4]"),(.@randomOptionAmount >= 5) ? GetRandomOptionId() : 0;
			// Slot
			.@itemSlot = getitemslots(.@itemId);
			// Card
			.@cardAmount = getarraysize($card2Ids);
			set getd("card" + .@r + "[0]"),(.@itemSlot >= 1) ? $card2Ids[rand(.@cardAmount)] : 0;
			set getd("card" + .@r + "[1]"),(.@itemSlot >= 2) ? $card2Ids[rand(.@cardAmount)] : 0;
			set getd("card" + .@r + "[2]"),(.@itemSlot >= 3) ? $card2Ids[rand(.@cardAmount)] : 0;
			set getd("card" + .@r + "[3]"),(.@itemSlot >= 4) ? $card2Ids[rand(.@cardAmount)] : 0;
			// Enchantment
			.@enchantmentAmount = getarraysize($enchant2Ids);
			set getd("enchantment" + .@r + "[0]"),(.@itemSlot < 1) ? $enchant2Ids[rand(.@enchantmentAmount)] : 0;
			set getd("enchantment" + .@r + "[1]"),(.@itemSlot < 2) ? $enchant2Ids[rand(.@enchantmentAmount)] : 0;
			set getd("enchantment" + .@r + "[2]"),(.@itemSlot < 3) ? $enchant2Ids[rand(.@enchantmentAmount)] : 0;
			set getd("enchantment" + .@r + "[3]"),(.@itemSlot < 4) ? $enchant2Ids[rand(.@enchantmentAmount)] : 0;
		}
	}
	freeloop(0);
	isWishGranted = 1;
	mes "ขอพรใหม่แล้วทั้งหมด " + maximumWishSet +" Set";
	close;
	
OnInit:
	waitingroom "ขอพร Set ใหม่",0;
	end;
}
