-	script	Events	-1,{
	end;
	
OnPCLoginEvent:
	donpcevent "เปลี่ยน Save Point::OnRefreshOnlineAmount";
	isWarping = 0;
	end;
	
OnPCLogoutEvent:
	donpcevent "เปลี่ยน Save Point::OnRefreshOnlineAmount";
	deltimer "กองไฟนิทรา::OnWarpToDream";
	deltimer "กองไฟนิทรา::OnSpawn";
	.@mapName$ = strcharinfo(3);
	if(!IsSafeMap() && isPartyLeader)
	{
		isWishGenerated = 0;
		dreamIndex = cap_value(dreamIndex - 1,0,1000);
		// Remove monster
		killmonsterall .@mapName$;
		// Remove from busy map
		.@busyMapIndex = inarray($busyMap$,.@mapName$);
		if(.@busyMapIndex != -1)
		{
			deletearray $busyMap$[.@busyMapIndex],1;
		}
		addrid(1);
		isPartyLeaderLeave = 1;
		// Warp map
		warp "Save",0,0;
	}
	end;
	
OnPCLoadMapEvent:
	if(IsSafeMap())
	{
		// Fill up Sleeping Pill
		FillUpSleepyPill();
		// Victory announcer
		if(isAnnounceInLobby)
		{
			mapannounce $town$,strcharinfo(0) + " สามารถเข้าสู่ชั้นความฝันที่ " + dreamIndex + " ได้แล้ว!",0,0xa981ff;
			isAnnounceInLobby = 0;
		}
		// Player leader die penalty hint
		if(isPartyLeaderDie)
		{
			isPartyLeaderDie = 0;
			mes "คุณตื่นขึ้นแล้ว เนื่องจากหัวหน้า Party หมดสติ..";
			close2;
		}
		// Player leader leave penalty hint
		if(isPartyLeaderLeave)
		{
			isPartyLeaderLeave = 0;
			mes "คุณตื่นขึ้นแล้ว เนื่องจากหัวหน้า Party ออกจากเกม ก่อนการฝันจบลง..";
			close2;
		}
		// Player leader load penalty hint
		if(isPartyLeaderLoad)
		{
			isPartyLeaderLoad = 0;
			mes "คุณตื่นขึ้นแล้ว เนื่องจากหัวหน้า Party ออกจากความฝัน ก่อนการฝันจบลง..";
			close2;
		}
		// Player leader leave party penalty hint
		if(isPartyLeaderLeaveParty)
		{
			isPartyLeaderLeaveParty = 0;
			mes "คุณตื่นขึ้นแล้ว เนื่องจากหัวหน้า Party ออกจาก Party ก่อนการฝันจบลง..";
			close2;
		}
		// Player member amount change hint
		if(isPartyAmountChange)
		{
			isPartyAmountChange = 0;
			mes "คุณตื่นขึ้นแล้ว เนื่องจากจำนวนสมาชิกใน Party เปลี่ยนแปลง..";
			close2;
		}
	}
	end;
	
OnPCDieEvent:
	.@mapName$ = strcharinfo(3);
	if(!IsSafeMap() && isPartyLeader)
	{
		isWishGenerated = 0;
		dreamIndex = cap_value(dreamIndex - 1,0,1000);
		// Remove monster
		killmonsterall .@mapName$;
		// Remove from busy map
		.@busyMapIndex = inarray($busyMap$,.@mapName$);
		if(.@busyMapIndex != -1)
		{
			deletearray $busyMap$[.@busyMapIndex],1;
		}
		addrid(1);
		isPartyLeaderDie = 1;
		// Warp map
		warp "Save",0,0;
	}
	end;
	
OnInit:
	bindatcmd "load",strnpcinfo(3) + "::OnLoad";
	addrid(0);
	dreamCd = 0;
	.@mapName$ = strcharinfo(3);
	if(!IsSafeMap())
	{
		warp "Save",0,0;
	}
	end;

OnLoad:
	PaWButterflyWing();
	end;
}

moc_akhet	mapflag	loadevent
